plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace = 'com.example.evcharging'
    compileSdk 36

    defaultConfig {
        applicationId "com.example.evcharging"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Read API key from local.properties
        Properties properties = new Properties()
        properties.load(project.rootProject.file('local.properties').newDataInputStream())
        manifestPlaceholders = [
                MAPS_API_KEY: properties.getProperty('MAPS_API_KEY', ''),
                BASE_URL    : properties.getProperty('BASE_URL', 'https://10.0.2.2/')
        ]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding = true
    }

    flavorDimensions "app_type"
    productFlavors {
        user {
            dimension "app_type"
            applicationIdSuffix ".user"
            versionNameSuffix "-user"
        }
        operator {
            dimension "app_type"
            applicationIdSuffix ".operator"
            versionNameSuffix "-operator"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation project(":auth")
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout

    // Additional dependencies for bottom navigation and UI
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.fragment:fragment:1.6.2'
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.room:room-runtime:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata:2.7.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // Google Maps
    implementation libs.play.services.maps
    implementation libs.play.services.location

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core
}

tasks.register("assembleDebugUnitTest") {
    dependsOn(
            "assembleOperatorDebugUnitTest",
            "assembleUserDebugUnitTest"
    )
}

tasks.register("assembleDebugAndroidTest") {
    dependsOn(
            "assembleOperatorDebugAndroidTest",
            "assembleUserDebugAndroidTest"
    )
}
